buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'com.github.triplet.play'

apply from: "signing.gradle"
apply from: "deploy.gradle"

project.ext.set("archivesBaseName", "reversatile")

apply plugin: 'io.fabric'

// Enable Crashlytics NDK reporting
crashlytics {
    enableNdk true
}


android {

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
        exclude 'README.txt'
    }


    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    useLibrary 'android.test.base'
    useLibrary 'android.test.mock'


    dexOptions {
        javaMaxHeapSize "4g"
    }
    defaultConfig {
        multiDexEnabled false
        versionName androidGitVersion.name()
        versionCode androidGitVersion.code()
        ndk {
            abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
        }
        minSdkVersion 14
        targetSdkVersion 28
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
        }
    }

    externalNativeBuild {
        ndkBuild {
            path 'src/main/jni/Android.mk'

        }
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "reversatile-${variant.name}-${variant.versionName}${variant.variantData.isSigned() ? "-signed" : "-unsigned"}.apk"
        }
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }

    lintOptions {
        disable 'GoogleAppIndexingWarning'
        disable 'UnusedAttribute'
    }
}

repositories {
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }

}

dependencies {

    implementation 'com.joanzapata.iconify:android-iconify-fontawesome:2.2.2' // (v4.5)

    api 'com.google.guava:guava:27.1-android'

    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:support-compat:28.0.0'
    implementation 'com.android.support:animated-vector-drawable:28.0.0'
    implementation 'com.android.support:support-v4:28.0.0'

    if(!project.hasProperty("noAnalytics")) {

        implementation 'com.google.firebase:firebase-core:16.0.9'
        implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
        implementation 'com.crashlytics.sdk.android:crashlytics-ndk:2.1.0'

    }
    // Required -- JUnit 4 framework
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    // Optional -- Mockito framework
    testImplementation 'org.mockito:mockito-core:2.28.2'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    // Optional -- Hamcrest library
    androidTestImplementation 'org.hamcrest:hamcrest-library:2.1'
}

apply plugin: 'com.google.gms.google-services'